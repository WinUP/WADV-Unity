// 类型转换以左侧表达式的运算符重载内容为主
// 变量取值顺序：当前域变量表->当前域常量表->上一层变量表->上一层常量表->再上一层……
// 变量为一级值传递，变量内所有非一级资源均为引用传递

@Utilities = ;import Utilities

@applicationName = @Utilities->ApplicationName

适用于工程"[@applicationName]"的VNS测试脚本。首先测试无角色对话。

@applicationName = @null

@#测试  = [Character Name="测试"]
@#测试2 = [Character Name="另一个测试" Avatar='AvatarBundle:Test2/Normal.png']

#测试 现在测试多角色的对话。
#测试2 如果这句话显示为"另一个测试"则没有问题。
\+这句话用来测试转义字符，本对话开头应该有一个+。\n这里应该显示在第二行上。
#测试 这里测试基本指令，首先暂停2秒。[Pause=2.0]然后等待点击。[Pause][Clear]之后把字号[Size=+5]加大并[Color=#808080]上色[/Color]然后回到[/Size]原始大小。
#测试 现在测试变量声明和赋值。[NoWait]

@temp = [Object 0=@#false 1=@#false]
@temp->3 = TEMP
@TEMP = true
@temp3 = @(@temp->3)

#测试2 [NoClear]变量的值应该为字符串true：[@temp3]

@temp3 = @TEMP = @temp = @#null

[Dialogue Character=测试2 Content=("这里测试可翻译的复杂对话，(1 + 2) * 3 = " + ((1 + 2) * 3))]
[Dialogue Character=测试2 Content=('这里测试不可翻译的复杂对话，(1.0 + 2) / 2 = ' + ((1.0 + 2) / 2))]
[Dialogue Character=(@#测试3 = [Character Name="第三个测试"]) Content=[Format "这里测试复合指令，这句话应该是由{角色}说的" 角色=@#测试3]]

@temp = !!@#测试3

#测试 这里测试自动变量，如果第三个测试成功，这里应该是true:[@temp]

@temp = @#null

;scene 测试场景1 @var1 @var2=1
    #测试 这里是测试场景1，var1的值为[@var1]，var2的值为[@var2]

#测试 下面测试场景变量传递，本次var1为1，var2为2。

;call 测试场景1 var1=1 var2=2

#测试 下面测试场景默认变量值，本次var1为1，var2为1。

;call 测试场景1 var1=1 

@temp = !(;if @SystemLanguage == default
    0
;else
    1
)


#测试 测试内联if指令，如果当前是默认语言这里显示1，否则显示0: [@temp]

@temp = @#null

    ;scene 测试场景2 @var1 @var2
        @var1 + @var2
    
    @temp = (;call 测试场景2 var1=1 var2=2)
        
    #测试 这是作用域内的场景返回值测试，这个值应该为3: [@temp]

#测试 由于刚才在作用域内，此时这里应该没有任何东西: [@temp]

@temp = 3
@temp2 = 1
;while @temp > 0
    @temp2 *= 2
    @temp -= 1

#测试 循环测试，这里应该为8: [@temp2]

@temp = @temp2 = @#null

#测试 具名传递和导出测试

@temp = 1
;scene 测试场景3 @temp
    @temp = 2
    ;return @temp2
;call 测试场景3 temp=@temp
#测试 这里应该是1: [@temp]

;scene Closure1
    @temp = 1
    ;scene Closure2
        @temp += 1

@runner = (;call Closure1)
@temp = (;call runner)
#测试 闭包测试，第一次为2: [@temp]
@temp = (;call runner)
#测试 闭包测试，第二次为3: [@temp]
@runner = @temp = @null

[Show DefaultTransform=[Transform AnchorCenter PivotCenter Scale=1 Rotate=0]]
[Color R=0 G=0 B=0 A=128]
[Color R=0.5 G=0.5 B=0.5 A=1.0]
[Color #FFFFFF]

[Show
    [Image Color=[Color #FFFFFF] FlipX FlipY UV=[Rect X=0 Y=0 Size=[Vector2 X=1 Y=1]] Source='Resources://Background/Classroom 2']
    Name=Background PositionX=100 PositionY=50
    
    [Image Color=0xFFFFFFFF FlipX FlipY UV=[Rect X=0 Y=0 Size=[Vector2 X=100 Y=100]] Source='Resources://Character/Tomo/1']
    Name=Left Transform=[Transform Scale=1.03 Rotate=[Vector3 X=0 Y=0 Z=45] Position=[Vector2 X=100 Y=200]]
    
    Effect=@Utilities->RoundFadeToLeft1.0Linear
    Bind=Container/Screen/Minimal/None]
