// 自动类型转换以右侧表达式类型为主
// 变量取值顺序：当前域变量表->当前域常量表->上一层变量表->上一层常量表->再上一层……
 
@init = (;call ((;import '/Logic/Init')->version))

VNS版本[@init]测试脚本。首先测试无角色对话。

[角色 编号=测试 显示名="测试"]
[角色 编号=测试2 显示名="另一个测试"]

#测试 现在测试多角色的对话。
#测试2 如果这句话显示为"另一个测试"则没有问题。
\+这句话用来测试转义字符，本对话开头应该有一个+。\n这里应该显示在第二行上。
#测试 这里测试基本指令，首先暂停2秒。[暂停=2000]然后等待点击。[暂停]之后把字号[字号=+5]加大并[颜色=#808080]上色[/颜色]然后回到[/字号]原始大小。
#测试 现在测试变量声明和赋值。

@temp = [对象 0=@#false 1=@#false 3=TEMP]
@TEMP = true
@temp3 = @(@temp->3)

#测试2 变量的值应该为true：[@temp3]

@true = @#false

#测试2 因为变量表里存在@true值为false，此时单纯的变量引用将得到false: [@true]，不过常量引用仍然是true: [@#true]

@temp3 = @TEMP = @temp= @true = @#null

[对话 角色=测试2 内容=("这里测试可翻译的复杂对话，(1 + 2) * 3 = " + ((1 + 2) * 3)) 广播]
[对话 角色=测试2 内容=('这里测试不可翻译的复杂对话，(1 + 2) / 2.0 = ' + ((1 + 2) / 2.0)) 广播]
[对话 角色=[角色 编号=测试3 显示名="第三个测试"] 内容=[格式化 模板="这里测试复合指令，这句话应该是由{角色}说的" 角色=@#测试3->显示名]]

@temp = !!@#测试3

#测试 这里测试自动变量，如果第三个测试成功，这里应该是true:[@temp]

@temp = @#null

;scene 测试场景1 @var1 @var2=1
    #测试 这里是测试场景1，var1的值为[@var1]，var2的值为[@var2]

#测试 下面测试场景变量传递，本次var1为1，var2为2。

;call 测试场景1 var1=1 var2=2

#测试 下面测试场景默认变量值，本次var1为1，var2为1。

;call 测试场景1 var1=1 

@temp = !(;if @SystemLanguage == default
    0
;else
    1
)


#测试 测试内联if指令，如果当前是默认语言这里显示1，否则显示0: [@temp]

@temp = @#null

    ;scene 测试场景2 @var1 @var2
        @var1 + @var2
    
    @temp = (;call 测试场景2 var1=1 var2=2)
        
    #测试 这是作用域内的场景返回值测试，这个值应该为3: [@temp]

#测试 由于刚才在作用域内，此时这里应该没有任何东西: [@temp]

@temp = 3
@temp2 = 1

;while @temp > 0
    @temp2 *= 2
    @temp -= 1

#测试 循环测试，这里应该为8: [@temp2]

@temp = @temp2 = @#null

@temp = 语言切换至 + (;lang en)
@temp2 = @#true
[切换语言 目标=en]

@temp = @temp + (;if @temp == en + @temp2
    0
;else
    1
) + 1

#测试 测试语言切换后是否指令仍旧可用，这里应该是1: [@temp]

@temp = @temp2 = @#null

#测试 具名传递和导出测试

// 函数作用域外的所有变量在函数中为只读模式，赋值会被重定向到新的同名局部变量上
@temp = 1
;scene 测试场景3 @temp
    @temp = 2
    ;return @temp2
;call 测试场景3 temp=@temp
#测试 这里应该是1: [@temp]

;scene Closure1
    @temp = 1
    ;scene Closure2
        [@temp->写入 值=(@temp + 1)]

@runner = (;call Closure1)
@temp = (;call runner)
#测试 闭包测试，第一次为2: [@temp]
@temp = (;call runner)
#测试 闭包测试，第二次为3: [@temp]
@runner = @temp = @null

// function Closure1() {
//     var temp = 1;
//     return function Closure2() {
//         return (temp += 1);
//     }
// }